apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'io.netty:netty-codec:4.0.44.Final'
    api 'io.netty:netty-handler:4.0.44.Final'

    compileOnly 'org.slf4j:slf4j-api:1.7.25'
    compileOnly 'com.google.android:android:4.1.1.4'
    compileOnly 'com.android.support:support-annotations:28.0.0'
}

sourceCompatibility = "7"
targetCompatibility = "7"


group 'com.virjar'
version '1.0.2-SNAPSHOT'


compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    [compileJava]*.options*.encoding = 'UTF-8'
}

compileTestJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    [compileTestJava]*.options*.encoding = 'UTF-8'
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}
// 生成 javadoc jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
// javadoc 配置，这里是自定义了 java doc 的一些配置
javadoc {
    description = "Generates project-level javadoc for use in -javadoc jar"

    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.version = true
    options.header = project.name
    options.addStringOption('Xdoclint:none', '-quiet')

    // suppress warnings due to cross-module @see and @link references;
    // note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
    options.encoding = "UTF-8"  //编码一定要配置否则直接出错
    options.charSet = 'UTF-8'
}

def getRepositoryUsername() {
    return hasProperty('MAVEN_USERNAME') ? MAVEN_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('MAVEN_PASSWORD') ? MAVEN_PASSWORD : ""
}


publishing {
    publications {
        // 这一个推送项目名称，mavenJava 相当于是一个 task name
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'sekiro-api'
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar


            // 添加 pom 相关信息
            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "Ratel API"
                description = "ratel api,used for developer on ratel system,an extension for xposed framewrok,ratel api compatable with original xposed framework"
                url = "https://github.com:virjar/sekiro"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    // 添加开发者描述，这个id不知道是什么
                    developer {
                        id = "weijia.deng"
                        name = "dengweijia"
                        email = "virjar@virjar.com"
                    }
                }
                // 添加你的 git 仓库 信息
                scm {
                    connection = "scm:git@github.com:virjar/sekiro.git"
                    developerConnection = "scm:git@github.com:virjar/sekiro.git"
                    url = "https://github.com:virjar/sekiro"
                }
            }
        }
    }
    repositories {
        // 添加一个远程仓库地址
        // releases 仓库
        maven {
            // 在对 task 中会生成对应的名称 publishMavenJavaPublicationToxxx
            // 后面的 xxx 就是你这里的名称，表示你要把jar 上传到这个仓库中
            name 'sonatypeRepository'  // 为你这个仓库起名
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
        // snapshots 仓库
        maven {
            name = 'sonatypeSnapshotRepository'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
    }
}
// 签名配置，注意这里的顺序，今天第一次知道 gradle 中的 task 等配置也是有顺序的
// 必须在 publishing 配置之后
signing {
    sign publishing.publications.mavenJava
}
